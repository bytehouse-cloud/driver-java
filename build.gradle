plugins {
    id 'java-library'
    id "io.freefair.lombok" version "5.1.1"
    id "jacoco"
    id "checkstyle"
    id "pmd"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group 'com.bytedance.bytehouse'
description 'ByteHouse JDBC Driver'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    maven {
        url "https://maven.byted.org/repository/public"
    }
    maven {
        url "https://maven.byted.org/repository/releases"
    }
    maven {
        url "https://maven.byted.org/repository/snapshots"
    }
}

dependencies {
    pmd 'com.alibaba.p3c:p3c-pmd:2.1.1'
    pmd "net.sourceforge.pmd:pmd-core:${pmdVersion}"
    pmd "net.sourceforge.pmd:pmd-java:${pmdVersion}"
    pmd "net.sourceforge.pmd:pmd-java8:${pmdVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    implementation "log4j:log4j:${log4jVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

shadowJar {
    zip64 true
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // Explicit strategy to exclude duplicates
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

checkstyle {
    toolVersion '8.36'
    configFile file("config/checkstyle/google_checks.xml")
}

pmd {
    sourceSets = [sourceSets.main]
    ruleSetFiles = files(
            "config/pmd/quickstart.xml",
            "config/pmd/p3c-ruleset.xml"
    )
    ruleSets = []
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        shadow(MavenPublication) {
            publication -> project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            name "nexus"
            def releasesRepoUrl = "https://maven.byted.org/repository/releases"
            def snapshotsRepoUrl = "https://maven.byted.org/repository/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "$bytedanceRepoUser"
                password = "$bytedanceRepoPassword"
            }
        }
    }
}
